---
title: "Tutorial 1: Walk-through of working models"
subtitle: "<html><div style='float:left'></div><hr color='#EB811B' size=1px width=796px></html>"
author: "Nan-Hung Hsieh"
date: "April 25, 2019"
output:
  xaringan::moon_reader:
    css: [default, metropolis, metropolis-fonts]
    nature:
          highlightLines: true
---

# Outline

## 1. Model file (`*.model.R`)

## 2. Input/Simulation file (`*.in.R`)

## 3. Tetrachloroethylene PBPK

## 4. Ethylbenzene PBPK

## 5. Demo & Exercise

???

One compartment model as an example


---

class:inverse, center, middle

# Model

---

# Syntax 

.code80[

```r
# Model description file (this is a comment)

<Global variable specifications>

* States = { 
  <state variables for the model, such as quantity> 
}
* Outputs = { 
  <output variables, such as concentration> 
}
* Inputs = { 
  <input variables, such as exposure dose>
}
Initialize {
  <Equations for initializing or scaling model parameters>
}
Dynamics {
  <Equations for computing derivatives of the state variables>
}
CalcOutputs {
  <Equations for computing output variables>
}
End. # mandatory ending keyword
```
]

???

Basically, we can seperate the model file to seven section.

One of the important thing I want to mention here is how to group the definithion of this variables, equation or parameters.

- States, outputs and input use curly bracket and equal sign because we can only put variable in these sections.

- Initialize, Dynamics, and CalcOutputs,

---

background-image: url(https://i.ibb.co/hcd45Ly/Screen-Shot-2019-04-23-at-9-41-44-AM.png)
background-size: 190px
background-position: 90% 5% 

# # Comments

- Brief description of this model
- What is the version number
- When you create this model
- Summarize the unit used in this model

```r
## Description ####
# 1-compartment model with 1st-order absorption rate and 
# linear elimination 
#
# version: 1
#
# Date: 04-25-2019
# 
# Units: 
# - time in hr
# - volumes in L
# - masses of substances in mg
# - concentrations of substances in mg/L
```

???

Other - creator

- In addition, you can use common to focus on which section you want to edit.

---

# States 

### `States` are variables for which a first-order differential equation is defined in the `Dynamics{}` section

</br>

```r
States = {A_central,  # Quantity in central compartment (mg)
          A_elim};    # ~        eliminated
```

???

The state variable is the quantity of the chemical in blood or body tissues.

All state variables must have the differential equation in the dynamics section.

---

# Outputs

### `Outputs` are dependent model variables (obtainable at any time as analytical functions of the states, inputs or parameters) that do not have dynamics. 

### They must receive assignments in either the `Dynamics{}` or `CalcOutputs{}` sections.

</br>

```r
Outputs = {C_central, # Concentration in central compartment (mg/l)
           A_total};  # Total quantity for mass balance
```

???

The outputs are used to determine which output variable that you will estimate. For example, the state variable is the quantity of the chemical in blood or body tissues.
The outputs section is used to define the estimated concentration.

A_total is not a state variable, but we use the capital A to declare that the unit of this variables is same as other state variables.   
  
---

# Inputs

### `Inputs` are variables independent of the others variables, but eventually varying with time (for example an exposure concentration to a chemical).

</br>

```r
Inputs = {Oral_input}; # Chemical input (mg)
```

???

The input section is used to describe the exposure, so the user can use the defined input that can be based on the exposure route include inhalation, oral intake, and dermal contact. 

---

class:clear

Global variable specifications


```r
# Chemical-specific parameter
Ke = 0.1;             # Elimination rate constant (1/h)
Pct_M_central = 0.05; # % body weight, fractional volume of distribution 

# Physiological-specific parameter
BW = 60; # Body weight (kg) 

# Exposure parameter
OralDose   = 100;  # Oral dose (mg/kg)
Period     = 12.0; # period of the exposure/no exposure cycle (h)
Tlag       = 0.0;  # Absorption lagtime (h)
Ka         = 0.1;  # Intestinal absorption rate constant (1/h)

# Scale parameter computed in Initialize
V_central; # Distribution volume of central compartment (L)
IngDose;   # Ingested dose (mg)

* Oral_input = PerExp (IngDose, Period, Tlag, Ka);
```

[*] The `Oral_input` can be defined in input file

???

This part is used to define the parameter that will be used in the model. It is used to define the default value of the input parameters. All parameters that will appear in the initialize and Dynamics section should be defined in this part.

---

# Initialize{}

### The equations given in this section will define a function (subroutine) that will be called by *GNU MCSim* after the assignments specified in each Simulation section are done.

</br>

```r
Initialize {  
  IngDose = BW * OralDose;
  V_central = BW * Pct_M_central; 
}
```

???

This Initilalize section is used to change the unit or estimate the parameter value baesd on the given condition.

---

# Dynamics{}

### The equations given in this section will be called by the integration routines at each integration step.

</br>

```r
Dynamics {
  dt (A_elim)    = Ke * A_central;
  dt (A_central) = Ka * Oral_input - Ke * A_central;
}

```

???

The dynamic section is used to specified the ordinary differential equation.

---

# CalcOutputs{}

### The equations given in this section will be called by the simulation program at each output time.

</br>

```r
CalcOutputs {
  C_central = A_central / V_central;
  A_total   = A_central + A_elim;
}
```

???

The last one is the calculated output for computing derivatives of the state variables. If the user has additional output variable that will put the equation to state the output in this section.

---

# Common pitfalls

```r
*** Error: End keyword is missing in file modeling/one.model.R.
One or more fatal errors: Exiting...
```

```r
*** Error: State variable 'A_central' has no dynamics.

State equations missing.

One or more fatal errors: Exiting...
```

```r
*** Error: Output variable 'A_total' is not computed anywhere.

Output equations missing.

One or more fatal errors: Exiting...
```

```r
*** Error: line 37: Undefined identifier 'Period'.
One or more fatal errors: Exiting...
```

```r
*** Error: Output variable 'l' is not computed anywhere.

Output equations missing.

One or more fatal errors: Exiting...
```

???

After you finish the model you'll need to compile the model code to the executable program. But if you make the mistake you will not be able to sucessfully compile your code.

Demo

---

class:inverse, center, middle

# Input / Simulation

---

# Syntax

```r
# Input-file (text after # are comments)
<Global assignments and specifications>
Simulation {
  <Local assignments and specifications>
  <Specifications for first simulation>
}
Simulation {
  <Local assignments and specifications>
  <Specifications for second simulation>
}
# Unlimited number of simulation specifications
End. # Mandatory End keyword. Everything after this line is ignored
```

???

This is an example of basic simulation. The only thing you need to do is provide the given condition in your simulation, which is the output time points and the output variables. 

---

# Input functions

These functions can use to different exposure types

.code60[
```r
- PerDose(): # specifies a periodic input of constant

    PerDose(<magnitude>, <period>, <initial-time>, <exposure-time>);

  
- PerExp(): # specifies a periodic exponential input.

    PerExp(<magnitude>, <period>, <initial-time>, <decay-constant>);  

  
- PerTransit(): models a delayed input mechanism  

    PerTransit(<magnitude>, <period>, <initial-time-in-period>, 
              <decay-constant>, <number-of-input-compartments>);  
    
              
- NDoses(): specifies a number of stepwise inputs of variable magnitude and their starting times
    
    NDoses(<n>, <list-of-magnitudes>, <list-of-initial-times>);


- Spikes(): specifies a number of instantaneous inputs of variable magnitude and their exact times of occurrence.
    
    Spikes(<n>, <list-of-magnitudes>, <list-of-times>);

```
]

???

- Here is the list of supported input function in MCSim that can be used to describe the different exposure type. 

- In today's model, we will use `PerExp` to describe the oral intake of drug.

- For per PBPK model, we will use `PerDose` to describe the inhalation exposure of perc. 

- In the EB case, we will use `NDoses`

- PerDose (regular), NDoses (iregular)

---

# Example: One-compartment model

```r
# ./mcsim.one.model.R.exe one.in.R

Integrate (Lsodes, 1e-12, 1e-15, 1);

Period  = 1E6;  # One-time dose
Ka      = 1.3;  
Pct_M_central = 1;

Simulation { # 1
  OralDose = 100; 
  BW = 60;
  PrintStep (Oral_input, A_central, A_elim, A_total, C_central, 0, 96, 0.5);
}

Simulation { # 2
  OralDose = 150;
  BW = 80;
  PrintStep (Oral_input, A_central, A_elim, A_total, C_central, 0, 96, 0.5);
}

End.
```

---

background-image: url(http://weknowmemes.com/wp-content/uploads/2013/03/programmer-problems.jpg)
background-size: 200px
background-position: 90% 90% 

# Common pitfalls

```r
Error: line 10: Expected <model-variable> before 'Kb'
Reading experiment 1.
Error: line 14: Bad definition of experiment 1
```

```r
Error: line 11: Expected ';' before 'S'.
Error: line 15: Unclosed level statement

Fatal errors.  Exiting.
```

???

Demo

---

class:inverse, middle, center
# Toxicokinetic Modeling of Tetrachloroethylene

.footnote[
.left[
Bois et al. (1996) - [Population toxicokinetics of tetrachloroethylene](https://link.springer.com/article/10.1007/s002040050284)  
Chiu and Bois (2006) - [Revisiting the population toxicokinetics of tetrachloroethylene](https://link.springer.com/article/10.1007/s00204-006-0061-9)
]]

---
# Content

### Goal:
- How much PERC is metabolized at high doses
- Population distribution

### Experimental data: 
- Expose 6 healthy volunteers to PERC (72 & 144 ppm) for four hours, then measure concentrations in blood and exhaled air for a week

### Model:
- 4-compartment model, metabolism in liver

---

class:clear
background-image: url(https://i.ibb.co/xHYnFfh/PERC.png)
background-size: 620px
background-position: 50% 50% 

???

Compartments are assumed to be homogeneous and distribution limited by blood flow. Pulmonary exchanges are modeled by assuming instantaneous equilibrium between alveolar air, venous blood and arterial blood.

---
# Model composition (1)
.code70[
```r
#----------------------------------------------------------------------
# perc.model
# A four compartment model of Tetrachloroethylene (PERC) toxicokinetics
#----------------------------------------------------------------------
# States are quantities of PERC and metabolite formed, they can be
# output
States = {Q_fat,        # Quantity of PERC in the fat (mg)
          Q_wp,         #   ...   in the well-perfused compartment (mg)
          Q_pp,         #   ...   in the poorly-perfused compartment (mg)
          Q_liv,        #   ...   in the liver (mg)
          Q_exh,        #   ...   exhaled (mg)
          Q_met};       # Quantity of metabolite formed (mg)
Outputs = {C_liv,               # mg/l in the liver
           C_alv,               # ... in the alveolar air
           C_exh,               # ... in the exhaled air
           C_ven,               # ... in the venous blood
           Pct_metabolized,     # % of the dose metabolized
           C_exh_ug};           # ug/l in the exhaled air
Inputs = {C_inh,                # Concentration inhaled (ppm)
          R_ing};               # Ingestion rate (mg/min)
```
]
---
# Model composition (2)
```r
# Constants
# =========
# Conversions from/to ppm: 72 ppm = .488 mg/l
PPM_per_mg_per_l = 72.0 / 0.488;
mg_per_l_per_PPM = 1/PPM_per_mg_per_l;

# Nominal parameter values
# ========================
# Units:
# Volumes: liter
# Time:    minute
# Vmax:    mg / minute
# Km:      mg
# Flows:   liter / minute
```
---
# Model composition (3-1)
```r
# Exposure modeling
# -----------------

InhMag   = 0.0; # inhaled concentration in ppm
Period   = 0.0; # period of the exposure/no exposure cycle
Exposure = 0.0; # exposure dutation within a period
C_inh    = PerDose (InhMag, Period, 0.0, Exposure);
IngDose  = 0.0; # ingested dose
```
---
# Model composition (3-2)
```r
# Physiological and pharmacokinetic parameters
# --------------------------------------------

LeanBodyWt = 55;    # lean body weight

# Percent mass of tissues with ranges shown
Pct_M_fat  = .16;   # % total body mass
Pct_LM_liv = .03;   # liver, % of lean mass
Pct_LM_wp  = .17;   # well perfused tissue, % of lean mass
Pct_LM_pp  = .70;   # poorly perfused tissue, recomputed in initialize 

# Percent blood flows to tissues
Pct_Flow_fat = .09;
Pct_Flow_liv = .34;
Pct_Flow_wp  = .50; # will be recomputed in initialize 
Pct_Flow_pp  = .07;
```
---
# Model composition (3-3)
```r
# Tissue/blood partition coeficients

PC_fat = 144;
PC_liv = 4.6;
PC_wp  = 8.7;
PC_pp  = 1.4;
PC_art = 12.0;
Flow_pul   = 8.0;    # Pulmonary ventilation rate (minute volume)
Vent_Perf = 1.14;    # ventilation over perfusion ratio
sc_Vmax = .0026;     # scaling coeficient of body weight for Vmax
Km = 1.0;
```
---
# Model composition (4)
```r
# The following parameters are calculated from the above values in
# the Scale section before the start of each simulation.
# They are left uninitialized here.

BodyWt = 0;
V_fat = 0;           # Actual volume of tissues
V_liv = 0;
V_wp  = 0;
V_pp  = 0;
Flow_fat = 0;        # Actual blood flows through tissues
Flow_liv = 0;
Flow_wp  = 0;
Flow_pp  = 0;
Flow_tot = 0;        # Total blood flow
Flow_alv = 0;        # Alveolar ventilation rate
Vmax = 0;            # kg/minute
```
---
# Model composition (5)
.code60[
```r
#---------------------------------------------------------
# Scale
# Scale certain model parameters and resolve dependencies between parameters. Generally the scaling involves a
# change of units, or conversion from percentage to actual units.
#---------------------------------------------------------
Initialize {

  # Volumes scaled to actual volumes
  BodyWt = LeanBodyWt / (1 - Pct_M_fat);
  V_fat  = Pct_M_fat  * BodyWt/0.92;        # density of fat = 0.92 g/ml
  V_liv  = Pct_LM_liv * LeanBodyWt;
  V_wp   = Pct_LM_wp  * LeanBodyWt;
  V_pp   = 0.9 * LeanBodyWt - V_liv - V_wp; # 10% bones
  
  # Calculate Flow_alv from total pulmonary flow
  Flow_alv = Flow_pul * 0.7;
  
  # Calculate total blood flow from the alveolar ventilation rate and the V/P ratio.
  Flow_tot = Flow_alv / Vent_Perf;
  
  # Calculate actual blood flows from total flow and percent flows 
  Flow_fat = Pct_Flow_fat * Flow_tot;
  Flow_liv = Pct_Flow_liv * Flow_tot;
  Flow_pp  = Pct_Flow_pp  * Flow_tot;
  Flow_wp  = Flow_tot - Flow_fat - Flow_liv - Flow_pp;
  
  # Vmax (mass/time) for Michaelis-Menten metabolism is scaled
  # by multiplication of bdw^0.7 
  Vmax = sc_Vmax * exp (0.7 * log (LeanBodyWt));
  
} # End of model initialization
```
]

---
# Model composition (6-1)
.font80[
```r
#---------------------------------------------------------
# Dynamics
# Define the dynamics of the simulation. This section is
# calculated with each integration step. It includes
# specification of differential equations.
#---------------------------------------------------------
Dynamics {

# Venous blood concentrations at the organ exit
Cout_fat = Q_fat / (V_fat * PC_fat);
Cout_wp  = Q_wp  / (V_wp  * PC_wp);
Cout_pp  = Q_pp  / (V_pp  * PC_pp);
Cout_liv = Q_liv / (V_liv * PC_liv);

# Sum of Flow * Concentration for all compartments
dQ_ven = Flow_fat * Cout_fat + Flow_wp * Cout_wp
         + Flow_pp * Cout_pp + Flow_liv * Cout_liv;

# Venous blood concentration
C_ven =  dQ_ven / Flow_tot;

# Arterial blood concentration
# Convert input given in ppm to mg/l to match other units
* C_art = (Flow_alv * C_inh / PPM_per_mg_per_l +  dQ_ven) / (Flow_tot + Flow_alv / PC_art);

```
]

---

# Model composition (6-2)

.font80[
```r
# Alveolar air concentration
C_alv = C_art / PC_art;

# Exhaled air concentration
C_exh = 0.7 * C_alv + 0.3 * C_inh / PPM_per_mg_per_l;

# Differentials
dt (Q_exh) = Flow_alv * C_alv;
dt (Q_fat) = Flow_fat * (C_art - Cout_fat);
dt (Q_wp)  = Flow_wp  * (C_art - Cout_wp);
dt (Q_pp)  = Flow_pp  * (C_art - Cout_pp);

# Quantity metabolized in liver
dQmet_liv = Vmax * Q_liv / (Km + Q_liv);
dt(Q_liv) = R_ing + Flow_liv * (C_art - Cout_liv) - dQmet_liv;

# Metabolite formation
dt (Qmet)  = dQmet_liv;
} # End of Dynamics
```
]

---

# Model composition (7)

```r
#---------------------------------------------------------
# CalcOutputs
# The following outputs are only calculated just before values
# are saved.  They are not calculated with each integration step.
#---------------------------------------------------------
CalcOutputs {

# Fraction of TCE metabolized per day

Pct_metabolized = (InhMag ?
                   Qmet / (1440 * Flow_alv * InhMag * mg_per_l_per_PPM):
                   0);
C_exh_ug  = C_exh * 1000; # milli to micrograms

} # End of output calculation

End.

```

---

class:inverse, middle, center

# Toxicokinetic Modeling of Ethylbenzene

---

class:inverse, middle, center

# Demo & Exercise

---

## Exercise 1

### Run EB-PBPK model under 100 ppm exposure for 4 hours and plot the time-course of blood concentration from 0 to 6 hour.

</br>

Input file: `EB_exercise_1.in.R`

.code60[

```r
# ./mcsim.EB.model.R.exe EB_exercise_1.in.R

Integrate (Lsodes, 1e-9, 1e-11 , 1);

Simulation { 
  # Inhalation concentration in ppm
  Cppm = NDoses (2, 100, 0, 0, 4 ); 
  PrintStep(Cvtot, 0, 6, 0.01);  
} 

End.

```

]

---

## Exercise 2

### Estimate the steady-state of arterial and venous blood concentrations associated with EB exposures (0.1 ppm to 1000 ppm).

---

class:clear

Input file: `EB_exercise_2.in.R`

.code60[

```r
# ./mcsim.EB.model.R.exe EB_exercise_2.in.R

Integrate (Lsodes, 1e-9, 1e-11 , 1);

Simulation { # 1 1 ppm
  # Inhalation concentration in ppm
  Cppm = NDoses (2, 1, 0, 0, 96 ); 
  PrintStep(Cart, Cvtot, 0, 96, 1);  
} 

Simulation { # 2 10 ppm
  # Inhalation concentration in ppm
  Cppm = NDoses (2, 10, 0, 0, 96 ); 
  PrintStep(Cart, Cvtot, 0, 96, 1);  
} 

Simulation { # 3 100 ppm
  # Inhalation concentration in ppm
  Cppm = NDoses (2, 100, 0, 0, 96 ); 
  PrintStep(Cart, Cvtot, 0, 96, 1);  
} 

Simulation { # 4 1000 ppm
  # Inhalation concentration in ppm
  Cppm = NDoses (2, 1000, 0, 0, 96 ); 
  PrintStep(Cart, Cvtot, 0, 96, 1);  
} 

End.

```
]

---

## Exercise 3

### Construct the relationships for the estimated inhalation exposure level and the fraction of EB metabolized after 8-hr continuous exposure (0.1 ppm to 1000 ppm). 

### In addition, estimate the percentage metabolized from **liver**, **lung**, and **richly perfused tissue**.

---

**Hint:** Add following information in model file:

```r
Amet_Rl, # Amount metabolized in liver (moles)
Amet_Rlu, # Amount metabolized in lung (moles)
Amet_Rvrg # Amount metabolized in richly perfused tissue (moles)

dt(Amet_Rl) = Rl;
dt(Amet_Rlu) = Rlu;
dt(Amet_Rvrg) = Rvrg;
```

---

class:clear

.code60[

Input file: `EB_exercise_3.in.R`

```r
# ./mcsim.EB_v2.model.R.exe EB_exercise_3.in.R

Integrate (Lsodes, 1e-9, 1e-11 , 1);

Simulation { # 1 1 ppm
  # Inhalation concentration in ppm
  Cppm = NDoses (2, 1, 0, 0, 8 ); 
  PrintStep(Ain, Amet_Rl, Amet_Rlu, Amet_Rvrg, Amet, 0, 8, 0.5);  
} 

Simulation { # 2 10 ppm
  # Inhalation concentration in ppm
  Cppm = NDoses (2, 10, 0, 0, 8 ); 
  PrintStep(Ain, Amet_Rl, Amet_Rlu, Amet_Rvrg, Amet, 0, 8, 0.5);  
} 

Simulation { # 3 100 ppm
  # Inhalation concentration in ppm
  Cppm = NDoses (2, 100, 0, 0, 8 ); 
  PrintStep(Ain, Amet_Rl, Amet_Rlu, Amet_Rvrg, Amet, 0, 8, 0.5);  
} 

Simulation { # 4 1000 ppm
  # Inhalation concentration in ppm
  Cppm = NDoses (2, 1000, 0, 0, 8 ); 
  PrintStep(Ain, Amet_Rl, Amet_Rlu, Amet_Rvrg, Amet, 0, 8, 0.5);  
} 

End.

```
]

---

## Exercise 4

### Add additional exposure routes include oral ingestion in the EB-PBPK model and estimate the Cmax and Tmax after received a single gavage dose of 180 mg/kg.

---

**Hint:** Add following information in model file:

```r
Dmgkg,      # Ingested dose (mg/kg)        
Dmgkg = 0.0;      # Ingested dose (mg/kg)
Aing = Dmgkg * BW * Cfac;
```

and input file

Input file: `EB_exercise_4.in.R`

.code60[
```r
# ./mcsim.EB_v2.model.R.exe EB_exercise_4.in.R

Simulation { # 1 
  Integrate (Lsodes, 1e-9, 1e-11 , 1);
  Dmgkg = PerExp(180, 1e2, 0.0, 1.0);
  PrintStep(Dmgkg, Cvtot, 0, 24, 0.1);  
} 

End.
```
]

